package(default_visibility = ["//visibility:public"])

licenses(["notice"])

load(
    "@build_bazel_rules_apple//apple:macos.bzl",
    "macos_application",
)
load(
    "@build_bazel_rules_swift//swift:swift.bzl",
    "swift_library",
)
load(
    "@build_bazel_rules_apple//apple:versioning.bzl",
    "apple_bundle_version",
)
load(
    "@com_github_bazelbuild_buildtools//buildifier:def.bzl",
    "buildifier",
)
load(
    "//third_party:repositories.bzl",
    "namespaced_name",
)

load(
    "//tools:xchammerconfig.bzl",
    "xchammer_config",
    "gen_xchammer_config",
    "project_config",
    "target_config",
    "execution_action",
    "scheme_action_config",
)

swift_library(
    name = "Sources",
    srcs = glob(["Sources/**/*.swift"]),
    structured_resources = glob([
        "XCHammerAssets/**",
    ]),
    deps = ["@" + namespaced_name(x) for x in [
        "AEXML//:AEXML",
        "Commandant//:Commandant",
        "Commander//:Commander",
        "JSONUtilities//:JSONUtilities",
        "PathKit//:PathKit",
        "Rainbow//:Rainbow",
        "Result//:Result",
        "ShellOut//:ShellOut",
        "Tulsi//src/TulsiGenerator:tulsi_generator_lib",
        "XcodeGen//:XcodeGenKit",
        "XcodeGen//:ProjectSpec",
        "xcproj//:xcproj",
        "Yams//:Yams",
    ]],
)

apple_bundle_version(
    name = "XCHammerVersion",
    build_version = "1.0",
)

macos_application(
    name = "xchammer",
    bundle_id = "com.pinterest.xchammer",
    infoplists = ["Info.plist"],
    minimum_os_version = "10.13",
    version = ":XCHammerVersion",
    deps = [":Sources"],
)

buildifier(
    name = "buildifier"
)

script_base = "$SRCROOT/tools/instrumentation_helpers"
scheme_config={
    "Build": scheme_action_config(
        pre_actions=[
            execution_action(
                name="Track build start",
                script=script_base + "/statsd_pre_build_action.sh"
            )
        ],
        post_actions=[
            execution_action(
                name="Report build end",
                script="python " + script_base + "/statsd_post_build_action.py"
            )
        ]
    )
}

xchammer_config = xchammer_config(
    targets=["//:xchammer"],
    target_config={
        "//:xchammer" : target_config(
            #scheme_config=scheme_config
        )
    },
    projects={ 
        "xchammer-bazel":
            project_config(paths=["**"])
    })

gen_xchammer_config(name="xchammer_config", config=xchammer_config)

